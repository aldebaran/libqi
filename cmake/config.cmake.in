@PACKAGE_INIT@

# Find dependencies
include(CMakeFindDependencyMacro)

# - Boost
find_dependency(
  Boost @BOOST_VERSION@ REQUIRED
  COMPONENTS @BOOST_COMPONENTS@
)

# - OpenSSL
find_dependency(OpenSSL @OPENSSL_VERSION@ REQUIRED)

# - Threads
if(@UNIX@)
  find_dependency(Threads REQUIRED)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@PACKAGE_NAME@-targets.cmake")

check_required_components(@PACKAGE_NAME@)

# qibuild compatiblity variables.
# These variables are deprecated and will be removed in the future.
set(global_target "@PACKAGE_NAME@::@PACKAGE_NAME@_global")
if(NOT TARGET "${global_target}")
  string(TOUPPER @PACKAGE_NAME@ PACKAGE_NAME_UPPER)

  # Create a global imported interface library so that any project can link to
  # it, even projects in other directories. This is required because qibuild saves
  # if a package has already been found by any CMake subproject as a global
  # property, meaning it will not retry to find the package in other
  # subdirectories. Consequently, the target needs to stay available in any
  # directory.
  add_library(
    ${global_target}
    INTERFACE IMPORTED GLOBAL
  )
  target_link_libraries(
    ${global_target}
    INTERFACE
      "@PACKAGE_NAME@::@PACKAGE_NAME@"
  )
  set(
    "${PACKAGE_NAME_UPPER}_LIBRARIES"
    ${global_target}
    CACHE INTERNAL "" FORCE
  )
endif()
